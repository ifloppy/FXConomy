plugins {
	id 'fabric-loom' version '1.9-SNAPSHOT'
	id 'maven-publish'
    id("com.gradleup.shadow") version "9.0.0-beta4"
}

version = project.version
group = project.group

base {
	archivesName = "XConomy-Fabric-${project.version}"
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
    mavenCentral()
    maven {
        url = 'https://jitpack.io'
    }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    implementation project(':XConomy-Core')
	
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
    from('../') {
        include 'LICENSE'
        into 'META-INF'
    }
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.base.archivesName.get()
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

shadowJar {
    dependencies {
        dependency("org.bstats:bstats-bukkit")
        dependency("com.zaxxer:HikariCP")
        dependency("redis.clients:jedis")
        dependency("org.apache.commons:commons-pool2")
    }
    exclude 'META-INF/versions/11/module-info*'
    exclude 'META-INF/MANIFEST.MF'
    relocate 'org.bstats', 'me.yic.xc_libs.bstats'
    relocate 'com.zaxxer', 'me.yic.xc_libs.zaxxer'
    relocate 'redis.clients', 'me.yic.xc_libs.redis'
    relocate 'org.apache.commons', 'me.yic.xc_libs.pool2'
    relocate 'me.yic.xconomy', 'me.yic.xconomy.fabric'
}