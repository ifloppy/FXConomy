plugins {
	id 'java'
	id 'fabric-loom' version '1.9-SNAPSHOT'
	id 'com.gradleup.shadow' version '9.0.0-beta4'
}

version = project.mod_version
group = project.maven_group

repositories {
	mavenCentral()
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
	maven {
		url = "https://maven.nucleoid.xyz/"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Include XConomy-Core
	api project(':XConomy-Core')
	include project(':XConomy-Core')

	// Add SnakeYAML dependency
	implementation 'org.yaml:snakeyaml:2.2'
	include 'org.yaml:snakeyaml:2.2'

	// Add Fabric Permissions API
	include(modImplementation('me.lucko:fabric-permissions-api:0.3.1'))

	// Add Common Economy API
	modImplementation include("eu.pb4:common-economy-api:1.1.1")
}

processResources {
	inputs.property "version", project.version
	filteringCharset "UTF-8"

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = 17
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withSourcesJar()
}

shadowJar {
	dependencies {
		include(project(':XConomy-Core'))
	}
	
	exclude 'META-INF/versions/11/module-info*'
	exclude 'META-INF/MANIFEST.MF'
	
	relocate 'org.bstats', 'me.yic.xc_libs.bstats'
	relocate 'com.zaxxer', 'me.yic.xc_libs.zaxxer'
	relocate 'redis.clients', 'me.yic.xc_libs.redis'
	relocate 'org.apache.commons', 'me.yic.xc_libs.pool2'
	relocate 'org.yaml.snakeyaml', 'me.yic.xc_libs.snakeyaml'
	
	minimize()
}

remapJar {
	dependsOn(shadowJar)
	mustRunAfter(shadowJar)
	inputFile = file(shadowJar.archivePath)
}