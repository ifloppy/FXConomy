plugins {
    id 'java'
    id("com.gradleup.shadow") version "9.0.0-beta4"
}

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url = 'https://repo.codemc.org/repository/maven-public'
    }
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = 'https://repo.glaremasters.me/repository/towny/'
    }
    maven {
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url = 'https://plugins.gradle.org/m2/'
    }
}

dependencies {
    //Spigot 1.17.1
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    //Vault
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    //Placeholder API
    compileOnly 'me.clip:placeholderapi:2.11.6'
    //BStats-Bukkit
    implementation 'org.bstats:bstats-bukkit:3.0.2'
    //Enterprise
    compileOnly 'com.github.the-h-team:Enterprise:1.7'
    //Towny
    compileOnly 'com.palmergames.bukkit.towny:towny:0.98.2.0'
    //XConomy-Core
    implementation project(':XConomy-Core')
}

processResources {
    from('../') {
        include 'LICENSE'
        into 'META-INF'
    }
    from('src/main/resources') {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

shadowJar {
    dependencies {
        exclude(dependency("org.bstats:bstats-bukkit"))
        exclude(dependency("com.zaxxer:HikariCP"))
        exclude(dependency("redis.clients:jedis"))
        exclude(dependency("org.apache.commons:commons-pool2"))
    }
    exclude 'META-INF/versions/11/module-info*'
    exclude 'META-INF/MANIFEST.MF'
    exclude 'me/yic/xconomy/XConomy.class'
    exclude 'me/yic/xconomy/data/ImportData.class'
    exclude 'me/yic/xconomy/task/ReceivePerCheck.class'
    exclude 'me/yic/xconomy/adapter/comp/*'
    exclude 'me/yic/xconomy/depend/*'
    relocate 'org.bstats', 'me.yic.xc_libs.bstats'
    relocate 'com.zaxxer', 'me.yic.xc_libs.zaxxer'
    relocate 'redis.clients', 'me.yic.xc_libs.redis'
    relocate 'org.apache.commons', 'me.yic.xc_libs.pool2'
}
