plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
}

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url = 'https://repo.codemc.org/repository/maven-public'
    }
    maven {
        url = 'https://jitpack.io'
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = 'https://repo.glaremasters.me/repository/towny/'
    }
    maven {
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    maven {
        url = 'https://plugins.gradle.org/m2/'
    }
}

dependencies {
    //SpongeAPI 7.2
    compileOnly 'org.spongepowered:spongeapi:7.2.0'
    //BStats-Sponge
    implementation 'org.bstats:bstats-sponge:2.2.1'
    //XConomy-Core
    implementation project(':XConomy-Core')
    compileOnly 'org.jetbrains:annotations:23.0.0'
}

processResources {
    from('../') {
        include 'LICENSE'
        into 'META-INF'
    }
    from('src/main/resources') {
        filter(org.apache.tools.ant.filters.ReplaceTokens)
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

tasks.register('processPluginInfo') {
    doLast {
        def pluginInfoFile = file('src/main/java-templates/PluginINFO.java')
        def outputFile = file('src/main/java/me/yic/xconomy/utils/PluginINFO.java')
        def content = pluginInfoFile.text.replace('${project.version}', project.version)
        outputFile.text = content
    }
}

compileJava.dependsOn(processPluginInfo)

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:-deprecation"
    options.compilerArgs << "-Xlint:-options"
}

shadowJar {
    dependencies {
        exclude(dependency("org.bstats:bstats-sponge"))
        exclude(dependency("com.zaxxer:HikariCP"))
        exclude(dependency("redis.clients:jedis"))
        exclude(dependency("org.apache.commons:commons-pool2"))
    }
    exclude 'META-INF/versions/11/module-info*'
    exclude 'META-INF/MANIFEST.MF'
    exclude 'me/yic/xconomy/XConomy.class'
    exclude 'me/yic/xconomy/adapter/comp/*'
    relocate 'org.bstats', 'me.yic.xc_libs.bstats'
    relocate 'com.zaxxer', 'me.yic.xc_libs.zaxxer'
    relocate 'redis.clients', 'me.yic.xc_libs.redis'
    relocate 'org.apache.commons', 'me.yic.xc_libs.pool2'
}
